grammar ufscar.compiladores2.Musy with org.eclipse.xtext.common.Terminals

generate musy "http://www.compiladores2.ufscar/Musy"

Midi:
	'MIDI' name = ID '{' 
		body = MidiBody 
	'}'
;

MidiBody: 
	{MidiBody}
	params += Parameter*
	chords += DeclaredChord*
	tracks += Track*
;

Parameter:
	pb = ParameterBeat | 
	pts = ParameterTimeSignature | 
	ptn = ParameterTimeNote | 
	ptp = ParameterTimePause | 
	po = ParameterOctave
;
	
	
ParameterBeat:
	('BPM' ':' beat = INT)
;
		
ParameterTimeSignature:
	('TIME_SIGNATURE' ':' tsig = TimeSignature)
;
	
ParameterTimeNote:	
	('TIME_NOTE' ':' tn = Tn)
;

ParameterTimePause:	
	('TIME_PAUSE' ':' tp = Tp)
;

ParameterOctave:
	('OCTAVE' ':' octave = INT)
;

DeclaredChord:
	'chord' '(' cp=ChordParams ')' name = ID  
;

Track:
	'TRACK' name = ID ':' i=Instrument '{'
		tbody = TrackBody
	'}'
;

TrackBody:
	bc += BodyComponent (',' bc += BodyComponent)*
;

BodyComponent:
	note = Note | block = Block | ch = [DeclaredChord] | interval = Interval
;

Block:
	'BLOCK' name = ID '{'
		blockbody = TrackBody
	'}'
;

TimeSignature:
	quantity= INT '/' note = INT
;

Tp:
	'sb'| 'm' | 'sm' | 'cl' | 'sc' | 'f' | 'sf'
;

Tn:
	Tp
;

//CustomNote:
//	Note '(' CustomNoteParam ')'
//;


ChordParams:
	(cnote+=Note) (',' cnote+=Note)*
;

NoteDotting:
	'.' | '..' | '...'
;

NoteLetter: 
	'C' | 'D' | 'E' | 'F' | 'G' | 'A' | 'B'
;

Accident:
	'#' | 'b'
;

Note: 
	nl=NoteLetter acc=Accident? ('(' cnp=CustomNoteParam ')')?
;
Interval:
	'I' '(' tempo = Tp ')'	
;

Instrument:
	'PIANO' | 'GUITAR' | 'ELECTRIC GUITAR' | 'BASS' | 'SAX' | 'VIOLIN' | 'DRUMS' | 'STRINGS'
;

CustomNoteParam:
	octave = INT ',' duration = Tn dots = NoteDotting? | duration = Tn dots = NoteDotting? ',' octave = INT |
	octave = INT | duration = Tn dots = NoteDotting?
;
